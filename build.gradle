plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.4'
    id 'io.spring.dependency-management' version '1.1.6'
    id "org.flywaydb.flyway" version "10.18.2"
    id 'io.freefair.lombok' version '8.10'
    id 'jacoco'
    id 'org.owasp.dependencycheck' version '10.0.4'
    id 'com.adarshr.test-logger' version '4.0.0' // optional, creates pretty output of unit tests (during `gradle test`)
    id 'org.sonarqube' version '5.1.0.4882'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'cde.chameleon.ChameleonApplication'
    }
}

group = 'cde.chameleon'
version = '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

sourceSets {
    intTest {
        compileClasspath += sourceSets.main.output + sourceSets.test.output
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output
    }
}

configurations {
    intTestImplementation.extendsFrom testImplementation
    intTestCompile.extendsFrom testCompile
    intTestRuntime.extendsFrom testRuntime
    intTestRuntimeOnly.extendsFrom testRuntimeOnly
    cucumberRuntime.extendsFrom intTestImplementation
}

dependencies {
    // starters
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // prod dependencies
    implementation 'org.postgresql:postgresql:42.7.4'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'
    implementation 'org.modelmapper:modelmapper:3.2.1'
    implementation 'org.flywaydb:flyway-core:10.18.2'
    implementation 'org.flywaydb:flyway-database-postgresql:10.18.2'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.18.0'
    implementation 'net.logstash.logback:logstash-logback-encoder:8.0'
    implementation 'org.apache.commons:commons-lang3:3.17.0'
    implementation 'commons-codec:commons-codec:1.17.1'
    implementation 'io.micrometer:micrometer-registry-prometheus:1.13.4'
    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    // dev tools
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // test dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'com.google.truth:truth:1.4.4'
    testImplementation 'org.mockito:mockito-core:5.13.0'
    testImplementation 'net.bytebuddy:byte-buddy:1.15.3' // required by mockito
    testImplementation 'com.tngtech.archunit:archunit-junit5:1.3.0'
    testCompileOnly 'org.projectlombok:lombok:1.18.34'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'

    // integration test dependencies
    intTestImplementation 'io.cucumber:cucumber-java:7.19.0'
    intTestImplementation 'io.cucumber:cucumber-spring:7.19.0'
    intTestImplementation 'org.keycloak:keycloak-authz-client:25.0.6'
    intTestImplementation 'net.masterthought:cucumber-reporting:5.8.2'
    intTestCompileOnly 'org.projectlombok:lombok:1.18.34'
    intTestAnnotationProcessor 'org.projectlombok:lombok:1.18.34'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.required = true
    }

    // exclude spring configuration classes from unit test coverage
    afterEvaluate {
        getClassDirectories().setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['cde/chameleon/ChameleonApplication.class', 'cde/chameleon/config/**'])
        }))
    }
}

// see: https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    format = 'ALL'
    suppressionFile = 'src/test/resources/owasp-dependency-check.xml'
    outputDirectory = project.layout.buildDirectory.get().asFile.absolutePath + '/reports/owasp'
    hostedSuppressions {
        validForHours = 10
    }
    if (System.properties.containsKey('dataDirectory')) {
        data {
            directory = project.projectDir.absolutePath + '/' + System.properties.get('dataDirectory')
        }
    }
    if (System.properties.containsKey('nvdApiKey')) {
        nvd {
            apiKey = System.properties.get('nvdApiKey')
        }
    }
}

tasks.register('intTest') {
    group = 'build'
    dependsOn assemble, intTestClasses
    doLast {
        javaexec {
            mainClass = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime
            classpath += sourceSets.main.output + sourceSets.test.output + sourceSets.intTest.output
            args = [
                    '--plugin', 'pretty',
                    '--glue', 'cde.chameleon', 'src/intTest/resources',
                    '--plugin', 'html:build/reports/cucumber/index.html',
                    '--plugin', 'json:build/reports/cucumber/cucumber.json',
                    '--plugin', 'junit:build/reports/cucumber/cucumber.xml'
            ]
        }
    }
}