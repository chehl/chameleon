plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.4'
    id 'io.spring.dependency-management' version '1.1.3'
    id "org.flywaydb.flyway" version "9.22.1"
    id 'io.freefair.lombok' version '8.4'
    id 'jacoco'
    id 'org.owasp.dependencycheck' version '8.4.0'
    id 'com.adarshr.test-logger' version '3.2.0' // optional, creates pretty output of unit tests (during `gradle test`)
    id 'org.sonarqube' version '4.4.1.3373'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'cde.chameleon.ChameleonApplication'
    }
}

group = 'cde.chameleon'
version = '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

sourceSets {
    intTest {
        compileClasspath += sourceSets.main.output + sourceSets.test.output
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output
    }
}

configurations {
    intTestImplementation.extendsFrom testImplementation
    intTestCompile.extendsFrom testCompile
    intTestRuntime.extendsFrom testRuntime
    intTestRuntimeOnly.extendsFrom testRuntimeOnly
    cucumberRuntime.extendsFrom intTestImplementation
}

dependencies {
    // starters
    implementation 'org.springframework.boot:spring-boot-starter-web:3.1.4'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:3.1.4'
    implementation 'org.springframework.boot:spring-boot-starter-security:3.1.4'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server:3.1.4'
    implementation 'org.springframework.boot:spring-boot-starter-actuator:3.1.4'
    implementation 'org.springframework.boot:spring-boot-starter-validation:3.1.4'

    // prod dependencies
    implementation 'org.postgresql:postgresql:42.6.0'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0'
    implementation 'org.modelmapper:modelmapper:3.1.1'
    implementation 'org.flywaydb:flyway-core:9.22.2'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.2'
    implementation 'net.logstash.logback:logstash-logback-encoder:7.4'
    implementation 'org.apache.commons:commons-lang3:3.13.0'
    implementation 'commons-codec:commons-codec:1.16.0'
    implementation 'io.micrometer:micrometer-registry-prometheus:1.11.4'
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    // dev tools
    developmentOnly 'org.springframework.boot:spring-boot-devtools:3.1.4'

    // test dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-test:3.1.4'
    testImplementation 'com.google.truth:truth:1.1.5'
    testImplementation 'org.mockito:mockito-core:5.6.0'
    testImplementation 'net.bytebuddy:byte-buddy:1.14.9' // required by mockito
    testImplementation 'com.tngtech.archunit:archunit-junit5:1.1.0'
    testCompileOnly 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'

    // integration test dependencies
    intTestImplementation 'io.cucumber:cucumber-java:7.14.0'
    intTestImplementation 'io.cucumber:cucumber-spring:7.14.0'
    intTestImplementation 'org.keycloak:keycloak-authz-client:22.0.4'
    intTestImplementation 'net.masterthought:cucumber-reporting:5.7.7'
    intTestCompileOnly 'org.projectlombok:lombok:1.18.30'
    intTestAnnotationProcessor 'org.projectlombok:lombok:1.18.30'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.required = true
    }

    // exclude spring configuration classes from unit test coverage
    afterEvaluate {
        getClassDirectories().setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['cde/chameleon/ChameleonApplication.class', 'cde/chameleon/config/**'])
        }))
    }
}

// see: https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    cveValidForHours = 10
    format = 'ALL'
    suppressionFile = 'src/test/resources/owasp-dependency-check.xml'
    outputDirectory = getBuildDir().getAbsolutePath() + '/reports/owasp'
}

tasks.register('intTest') {
    group = 'build'
    dependsOn assemble, intTestClasses
    doLast {
        javaexec {
            mainClass = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime
            classpath += sourceSets.main.output + sourceSets.test.output + sourceSets.intTest.output
            args = [
                    '--plugin', 'pretty',
                    '--glue', 'cde.chameleon', 'src/intTest/resources',
                    '--plugin', 'html:build/reports/cucumber/index.html',
                    '--plugin', 'json:build/reports/cucumber/cucumber.json',
                    '--plugin', 'junit:build/reports/cucumber/cucumber.xml'
            ]
        }
    }
}