plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.3'
    id 'io.spring.dependency-management' version '1.1.0'
    id "org.flywaydb.flyway" version "9.15.1"
    id 'io.freefair.lombok' version '6.6.3'
    id 'jacoco'
    id 'org.owasp.dependencycheck' version '8.1.2'
    id 'com.adarshr.test-logger' version '3.2.0' // optional, creates pretty output of unit tests (during `gradle test`)
    id 'org.sonarqube' version '4.0.0.2929'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'cde.chameleon.ChameleonApplication'
    }
}

group = 'cde.chameleon'
version = '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

sourceSets {
    intTest {
        compileClasspath += sourceSets.main.output + sourceSets.test.output
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output
    }
}

configurations {
    intTestImplementation.extendsFrom testImplementation
    intTestCompile.extendsFrom testCompile
    intTestRuntime.extendsFrom testRuntime
    intTestRuntimeOnly.extendsFrom testRuntimeOnly
    cucumberRuntime.extendsFrom intTestImplementation
}

dependencies {
    // starters
    implementation 'org.springframework.boot:spring-boot-starter-web:3.0.3'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:3.0.4'
    implementation 'org.springframework.boot:spring-boot-starter-security:3.0.3'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server:3.0.3'
    implementation 'org.springframework.boot:spring-boot-starter-actuator:3.0.3'
    implementation 'org.springframework.boot:spring-boot-starter-validation:3.0.4'

    // prod dependencies
    implementation 'org.postgresql:postgresql:42.5.4'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2'
    implementation 'org.modelmapper:modelmapper:3.1.1'
    implementation 'org.flywaydb:flyway-core:9.15.1'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.14.2'
    implementation 'net.logstash.logback:logstash-logback-encoder:7.3'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'commons-codec:commons-codec:1.15'
    implementation 'io.micrometer:micrometer-registry-prometheus:1.10.4'
    compileOnly 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'

    // dev tools
    developmentOnly 'org.springframework.boot:spring-boot-devtools:3.0.3'

    // test dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-test:3.0.3'
    testImplementation 'com.google.truth:truth:1.1.3'
    testImplementation 'org.mockito:mockito-core:5.1.1'
    testImplementation 'net.bytebuddy:byte-buddy:1.14.0' // required by mockito
    testImplementation 'com.tngtech.archunit:archunit-junit5:1.0.1'
    testCompileOnly 'org.projectlombok:lombok:1.18.26'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.26'

    // integration test dependencies
    intTestImplementation 'io.cucumber:cucumber-java:7.11.1'
    intTestImplementation 'io.cucumber:cucumber-spring:7.11.1'
    intTestImplementation 'org.keycloak:keycloak-authz-client:21.0.1'
    intTestImplementation 'net.masterthought:cucumber-reporting:5.7.5'
    intTestCompileOnly 'org.projectlombok:lombok:1.18.26'
    intTestAnnotationProcessor 'org.projectlombok:lombok:1.18.26'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.required = true
    }

    // exclude spring configuration classes from unit test coverage
    afterEvaluate {
        getClassDirectories().setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['cde/chameleon/ChameleonApplication.class', 'cde/chameleon/config/**'])
        }))
    }
}

// see: https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    cveValidForHours = 10
    format = 'ALL'
    suppressionFile = 'src/test/resources/owasp-dependency-check.xml'
    outputDirectory = getBuildDir().getAbsolutePath() + '/reports/owasp'
}

tasks.register('intTest') {
    group = 'build'
    dependsOn assemble, intTestClasses
    doLast {
        javaexec {
            mainClass = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime
            classpath += sourceSets.main.output + sourceSets.test.output + sourceSets.intTest.output
            args = [
                    '--plugin', 'pretty',
                    '--glue', 'cde.chameleon', 'src/intTest/resources',
                    '--plugin', 'html:build/reports/cucumber/index.html',
                    '--plugin', 'json:build/reports/cucumber/cucumber.json',
                    '--plugin', 'junit:build/reports/cucumber/cucumber.xml'
            ]
        }
    }
}