buildscript {
    apply from: 'dependencies.gradle'
}

plugins {
    id 'java'
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'io.spring.dependency-management' version "${springDependencyManagementVersion}"
    id "org.flywaydb.flyway" version "${flywayVersion}"
    id 'io.freefair.lombok' version "${lombokPluginVersion}"
    id 'jacoco'
    id 'org.owasp.dependencycheck' version "${dependencycheckVersion}"
    id 'com.adarshr.test-logger' version "${testLoggerVersion}" // optional, creates pretty output of unit tests (during `gradle test`)
    id 'org.sonarqube' version "${sonarqubeVersion}"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'cde.chameleon.ChameleonApplication'
    }
}

group = 'cde.chameleon'
version = '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

sourceSets {
    intTest {
        compileClasspath += sourceSets.main.output + sourceSets.test.output
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output
    }
}

configurations {
    intTestImplementation.extendsFrom testImplementation
    intTestCompile.extendsFrom testCompile
    intTestRuntime.extendsFrom testRuntime
    intTestRuntimeOnly.extendsFrom testRuntimeOnly
    cucumberRuntime.extendsFrom intTestImplementation
}

dependencies {
    // starters
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-flyway'


    // prod dependencies
    implementation "org.postgresql:postgresql:${postgresVersion}"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springdocVersion}"
    implementation "org.modelmapper:modelmapper:${modelmapperVersion}"
    implementation "org.flywaydb:flyway-core:${flywayVersion}"
    implementation "org.flywaydb:flyway-database-postgresql:${flywayVersion}"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"
    implementation "net.logstash.logback:logstash-logback-encoder:${logstashVersion}"
    implementation "org.apache.commons:commons-lang3:${commonsLangVersion}"
    implementation "commons-codec:commons-codec:${commonsCodecVersion}"
    implementation "io.micrometer:micrometer-registry-prometheus:${micrometerRegistryVersion}"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    // dev tools
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // test dependencies
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "com.google.truth:truth:${truthVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "net.bytebuddy:byte-buddy:${byteBuddyVersion}" // required by mockito
    testImplementation "com.tngtech.archunit:archunit-junit5:${junitVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    // integration test dependencies
    intTestImplementation "io.cucumber:cucumber-java:${cucumberVersion}"
    intTestImplementation "io.cucumber:cucumber-spring:${cucumberVersion}"
    intTestImplementation "org.keycloak:keycloak-authz-client:${keycloakVersion}"
    intTestImplementation "net.masterthought:cucumber-reporting:${cucumberReportingVersion}"
    intTestCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    intTestAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.required = true
    }

    // exclude spring configuration classes from unit test coverage
    afterEvaluate {
        getClassDirectories().setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['cde/chameleon/ChameleonApplication.class', 'cde/chameleon/config/**'])
        }))
    }
}

// see: https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    format = 'ALL'
    suppressionFile = 'src/test/resources/owasp-dependency-check.xml'
    outputDirectory = project.layout.buildDirectory.get().asFile.absolutePath + '/reports/owasp'
    hostedSuppressions {
        validForHours = 10
    }
    if (System.properties.containsKey('dataDirectory')) {
        data {
            directory = project.projectDir.absolutePath + '/' + System.properties.get('dataDirectory')
        }
    }
    if (System.properties.containsKey('nvdApiKey')) {
        nvd {
            apiKey = System.properties.get('nvdApiKey')
        }
    }
}

task intTest(type: JavaExec) {
    group = 'build'
    dependsOn assemble, intTestClasses
    mainClass = "io.cucumber.core.cli.Main"
    classpath = configurations.cucumberRuntime
    classpath += sourceSets.main.output + sourceSets.test.output + sourceSets.intTest.output
    args = [
            '--plugin', 'pretty',
            '--glue', 'cde.chameleon', 'src/intTest/resources',
            '--plugin', 'html:build/reports/cucumber/index.html',
            '--plugin', 'json:build/reports/cucumber/cucumber.json',
            '--plugin', 'junit:build/reports/cucumber/cucumber.xml'
    ]
}